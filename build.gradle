plugins {
  id 'doki-theme-plugin'
  id 'org.jetbrains.intellij' version '0.4.16'
  id 'org.jetbrains.kotlin.jvm' version '1.3.60-eap-143'
  id "org.jlleitschuh.gradle.ktlint" version "8.2.0"
  id 'org.kordamp.gradle.markdown' version '2.2.0'
}

group 'io.unthrottled'
version '9.1.1'

repositories {
  maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
  mavenCentral()
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation 'commons-io:commons-io:2.6'
  implementation 'org.javassist:javassist:3.21.0-GA'
  implementation 'io.sentry:sentry:1.7.30'
}

configurations {
  implementation.exclude group: 'org.slf4j'
}

intellij {
  version '2020.1'
  type 'IU'
  alternativeIdePath idePath
}
compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

markdownToHtml {
  sourceDir file("$projectDir/changelog")
  outputDir file("$projectDir/build/html")
}

patchPluginXml {
  def releaseNotes ="$projectDir/build/html/RELEASE-NOTES.html"
  if(file(releaseNotes).exists()){
    changeNotes file(releaseNotes).text
  }
  untilBuild untilBuildVersion
}
tasks.patchPluginXml.dependsOn("markdownToHtml")
tasks.patchPluginXml.dependsOn("buildThemes")
